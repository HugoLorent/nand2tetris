// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Process x according to zx
    Mux16(a=x, b=false, sel=zx, out=xAfterZx);

    // Process x according to nx
    Not16(in=xAfterZx, out=notX);
    Mux16(a=xAfterZx, b=notX, sel=nx, out=xFinal);

    // Process y according to zy
    Mux16(a=y, b=false, sel=zy, out=yAfterZy);

    // Process y according to ny
    Not16(in=yAfterZy, out=notY);
    Mux16(a=yAfterZy, b=notY, sel=ny, out=yFinal);

    // Compute x&y
    And16(a=xFinal, b=yFinal, out=xAndY);

    // Compute x+y
    Add16(a=xFinal, b=yFinal, out=xPlusY);

    // Select based on f
    Mux16(a=xAndY, b=xPlusY, sel=f, out=fResult);

    // Potentially negate the output
    Not16(in=fResult, out=notFResult);
    Mux16(a=fResult, b=notFResult, sel=no, out[0..15]=out, out[15]=ng, out[0..7]=outLow, out[8..15]=outHigh);

    // Check if result is zero
    Or8Way(in=outLow, out=orLow);
    Or8Way(in=outHigh, out=orHigh);
    Or(a=orLow, b=orHigh, out=orResult);
    Not(in=orResult, out=zr);
}
